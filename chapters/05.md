# Chapter 5

## Creating And Structuring Haskell Projects

```bash
What does the package build:
1. Executable
2. Library
3. Library and Executable
```

This first prompt is asking you what kind of project you’d like to create. For libraries, pick the Library option. If you’re building an application, however, it’s common to use the Library and Executable project rather than just Executable. This supports the common Haskell idiom of creating a very small minimal executable application and putting most of the application logic in a library. Separating out the logic into a library makes testing easier.

## Understanding the Cabal File Format

```cabal
-- This is a comment top level comment
-- This is called stanza
library
-- this is an indented comment
    exposed-modules: MyLib -- This comment will cause an error
```

## GHC options

-O0 No optimizations
-O1 Common optimizations
-O2 Thorough optimizations
-prof Profiling
or
cabal configure --enable-profiling To make it project-wise

## Building and Running Your program

```bash
cabal build
cabal build lib:learn-cabal
cabal build exe:learn-cabal
cabal exec learn-cabal
cabal run learn-cabal -- build & run

```

## Using Code from Othe Modules

```haskell
module Main where
import Data.Char
import qualified MyLib (countNonPrintableCharacters)
```

## Local Module Aliases

```haskell
-- can be imported more than once
import Data.Text as T
import Data.Text as Text

Data.Text.pack
T.pack
Text.pack
```

```haskell
-- can also be imported under the same alias to group related imports
import Data.Text as T
import Data.Text.Encoding as T
```

## Qualified Imports

Tells compiler to use the whole path (can also be combined with aliases and list imports)

```haskell
import qualified Data.Text

Data.Text.pack
-- but not the other way around
-- pack

```

## Choosing What Not to Import

```haskell
import Data.Text hiding (length, filter)
import qualified Data.Text as T (length, filter)
```

## Creating Your Own Modules

1. Create folder under .cabal->Library->hs-source-dirs
2. Name module according file path, ex. module ModuleExamples.Folder1.Folder2.Module
3. Add full module name to .cabal->Library->exposed-modules

## Smart constructors

```haskell
module ModuleExamples.SortedList
  ( SortedList (getSorted),
    makeSortedList,
    minimum,
  )
where

import Data.List (sort)
import Prelude hiding (minimum)

data SortedList = SortedList {getSorted :: [Int]}

makeSortedList :: [Int] -> Maybe SortedList
makeSortedList [] = Nothing
makeSortedList numbers = Just $ SortedList (sort numbers)

minimum :: SortedList -> Int
minimum (SortedList numbers) = head numbers
```

## Phantom Types and Export Lists

Types that don’t have any corresponding values are called phantom types `Authenticated` and `Unauthenticated`:

```haskell
module ModuleExamples.UserInfo
  ( User,
    lookupUser,
    authenticate,
    getUserName,
    getUserScore,
    getUserEmailAddress,
  )
where

import Data.List (find)

data Authenticated

data Unauthenticated

data User isAuthenticated = User
  { userName :: String,
    userInternetPoints :: Int,
    userPassword :: String,
    userEmailAddress :: String
  }

users :: [User Unauthenticated]
users = [george, porter]
  where
    george =
      User
        { userName = "george",
          userInternetPoints = 1000,
          userPassword = "secret",
          userEmailAddress = "gbird2015@example.com"
        }
    porter =
      User
        { userName = "porter",
          userInternetPoints = 500,
          userPassword = "hunter2",
          userEmailAddress = "woofwoof@example.com"
        }

lookupUser :: String -> Maybe (User Unauthenticated)
lookupUser name =
  find (\user -> userName user == name) users

getUserName :: User isAuthenticated -> String
getUserName = userName

getUserScore :: User isAuthenticated -> Int
getUserScore = userInternetPoints

getUserEmailAddress :: User Authenticated -> String
getUserEmailAddress = userEmailAddress

authenticate :: User Unauthenticated -> String -> Maybe (User Authenticated)
authenticate User {..} password
  | userPassword == password = Just User {..}
  | otherwise = Nothing
```

E.g. getting user's email while not being authenticated will result in an type error

## Re-Exporting Code from Other Modules

```haskell
-- lib/Index.hs
module ModuleExamples.Index
  ( module ModuleExamples.UserInfo,
    module CreatingModules,
    SortedList.SortedList (..),
    SortedList.makeSortedList,
    SortedList.minimum,
  )
where

import ModuleExamples.Module as CreatingModules hiding
  ( testMessage,
  )
import ModuleExamples.SortedList qualified as SortedList
import ModuleExamples.UserInfo
```

```haskell
-- lib/ModuleDemo.hs
georgesEmailAddress :: Maybe String
georgesEmailAddress =
  case Examples.lookupUser "george" of
    Nothing ->
      Nothing
    Just unathenticatedGeorge ->
      case Examples.authenticate unathenticatedGeorge "secret" of
        Nothing -> Nothing
        Just george ->
          Just $ Examples.getUserEmailAddress george

friendlyEmail :: String -> String
friendlyEmail emailAddress =
  Examples.formatMessage
    Examples.GreetingMessage
      { Examples.greetingSalutation = Examples.Salutation "Hello",
        Examples.greetingTo = Examples.Name emailAddress,
        Examples.greetingFrom = [Examples.Name "mailer daemon"]
      }

demo :: String
demo =
  maybe "unknown user" friendlyEmail georgesEmailAddress
```
