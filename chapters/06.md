Chapter 6

## Creating And Structuring Haskell Projects

Object-oriented programming languages let you solve this problem using inheritance or interfaces to write different implementations of functions depending on their type. The idea that we can provide a different implementation of a function depending on its type is called ad hoc polymorphism. Haskell provides its own approach to ad hoc polymorphism with type classes

## Using Ad Hoc Polymorphism with Type classes

```haskell
data Natural a = Natural
  { equal :: a -> a -> Bool,
    add :: a -> a -> a,
    multiply :: a -> a -> a,
    additiveIdentity :: a,
    multiplicativeIdentity :: a,
    displayAsString :: a -> String
  }

intNatural :: Natural Int
intNatural =
  Natural
    { equal = (==),
      add = (+),
      multiply = (*),
      additiveIdentity = 0,
      multiplicativeIdentity = 1,
      displayAsString = show
    }

data Peano = Z | S Peano

toPeano :: Int -> Peano
toPeano 0 = Z
toPeano n = S $ toPeano (n - 1)

fromPeano :: Peano -> Int
fromPeano Z = 0
fromPeano (S n) = 1 + fromPeano n

peanoNatural :: Natural Peano
peanoNatural =
  Natural
    { equal = comparePeano,
      add = addPeano,
      multiply = multiplyPeano,
      -- In mathematics, the additive identity of a set that is equipped with the operation of addition is an element which, when added to any element x in the set, yields x. One of the most familiar additive identities is the number 0 from elementary mathematics, but additive identities occur in other mathematical structures where addition is defined, such as in groups and rings.
      additiveIdentity = Z,
      -- Multiplicative identity. noun. : an element of a set that when multiplied by any other element of the set leaves the element unchanged.
      multiplicativeIdentity = S Z,
      displayAsString = show . fromPeano
    }
  where
    comparePeano Z Z = True
    comparePeano (S a) (S b) = comparePeano a b
    comparePeano _ _ = False
    addPeano Z b = b
    addPeano (S a) b = addPeano a (S b)
    multiplyPeano Z _ = Z
    multiplyPeano (S a) b =
      addPeano b (multiplyPeano a b)
```

## Creating a Type Class for Natural

```haskell
class Natural n where
  equal :: n -> n -> Bool
  add :: n -> n -> n
  multiply :: n -> n -> n
  additiveIdentity :: n
  multiplicativeIdentity :: n
  displayAsString :: n -> String

instance Natural Int where
  equal = (==)
  add = (+)
  multiply = (*)
  additiveIdentity = 0
  multiplicativeIdentity = 1
  displayAsString = show
```

## Composing Type Classes

With this constraint, we’re requiring that anything that has an instance of Natural should also have an instance of Eq.

```haskell
class Eq a where
  (==) :: a -> a -> Bool

class (Eq n) => Natural n where
  add :: n -> n -> n
  multiply :: n -> n -> n
  additiveIdentity :: n
  multiplicativeIdentity :: n
  displayAsString :: n -> String
```

Or, with multiple constraints:

```haskell
class (Show n, Eq n) => Natural n where
  add :: n -> n -> n
  multiply :: n -> n -> n
  additiveIdentity :: n
  multiplicativeIdentity :: n
```

Defining equality and show-ity for Peano:

```haskell
instance Eq Peano where
  (==) Z Z = True
  (==) (S a) (S b) = a == b
  (==) _ _ = False
instance Show Peano where
  show Z = "Z"
  show (S a) = "(S " <> show a <> ")"
```

## Creating Default Implementations and Minimal Definitions

```haskell
instance Ord Word8 where
  compare a b
  | a == b = EQ
  | a == 0 = LT
  | b == 0 = GT
  | otherwise = compare (a - 1) (b - 1)
```

Or implement type class:

```haskell
class (Eq a) => Ord a where
  compare :: a -> a -> Ordering
  (<) :: a -> a -> Bool
  a < b =
    case compare a b of
      LT -> True
      _ -> False
  (<=) :: a -> a -> Bool
  a <= b =
    case compare a b of
      GT -> False
      _ -> True
  (>) :: a -> a -> Bool
  a > b =
    case compare a b of
      GT -> True
      _ -> False
  (>=) :: a -> a -> Bool
  a >= b =
    case compare a b of
      LT -> False
      _ -> True
  max :: a -> a -> a
  max a b =
    case compare a b of
      GT -> a
      _ -> b
  min :: a -> a -> a
  min a b =
    case compare a b of
      LT -> a
      _ -> b
```

And one can either instantiate type class:

```haskell

instance Ord Word8 where
  compare a b
    | a == b = EQ
    | a == 0 = LT
    | b == 0 = GT
    | otherwise = compare (a - 1) (b - 1)
```

Or write definition within type class:

```haskell
class Eq a => Ord a where
  compare :: a -> a -> Ordering
  compare a b
    | a == b = EQ
    | a <= b = LT
    | otherwise = GT
-- ...
-- is required because <= points to compare and compare to <=
-- to mitigate circular dependency a MINIMAL pragma is used
{-# MINIMAL compare | (<=) #-}
```

## Signatures for Default Instance Definitions

If there is a type class which instance does not have function needed to be implemented aside from other classes - no need to write empty where clause:

```haskell
class Show a => Redacted a where
  redacted :: a -> String
  redacted = show
data UserName = UserName String
instance Show UserName where
  show (UserName userName) = userName
instance Redacted UserName
```

To have Redacted instances not requiring Show and only redacted do:

```haskell
{-# LANGUAGE DefaultSignatures #-}
class Redacted a where
  redacted :: a -> String
  default redacted :: Show a => a -> String
  redacted = show
```

And if trying to print out data without defined show function:

```haskell
class Redacted a where
  redacted :: a -> String
  default redacted :: Show a => a -> String
  redacted = show

data UserName = UserName String

instance Show UserName where
  show (UserName userName) = userName

instance Redacted UserName

data Password = Password String

instance Redacted Password where
  redacted _ = "<redacted>"
```

Just as we’d hoped, UserName provides both a Show and Redacted instance that work the same, since we fall back to show when we haven’t provided a new definition for redacted. When we’re working with Password values we don’t have a Show instance, and so we can’t print the values out in plain text, but we can use our own definition of redacted to get a safe masked value.

## Specifying Type Class Instances with Type Applications

The TypeApplications language extension allows us to do exactly that. Type applications gives you the ability to pass type names as arguments to polymorphic functions, to select the type class instance that’s used.

```haskell
read :: Read a => String -> a
read @Integer "1"
read @Float "1"

-- to skip not required type application
fn @_ @Int
fn @Int
```

We’d like to be able to tell GHC that, whatever type it uses to instantiate a, that should also be the instance that it uses for the calls to read and show.

```haskell
adheresToReadShowContract :: forall a. (Read a, Show a) => a -> Bool
adheresToReadShowContract val =
  let a = show . read @a . show $ val
      b = show val
   in a == b
```

The use of forall here is introducing explicit universal quantification. This isn’t a term you’ll often need to use, except perhaps when reading some specific GHC documentation. More generally, it’s simply referred to as explicit forall.
